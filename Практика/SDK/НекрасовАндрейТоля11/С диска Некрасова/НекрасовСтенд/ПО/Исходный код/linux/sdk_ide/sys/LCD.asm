#INCLUDE	"DEFINE.ASM"

;************ НАЧАЛО ДРАЙВЕРа ОПРОСА КЛАВИАТУРЫ ****************
;############ОПИСАНИЕ######
;Данный драйвер состоит из двух подпрограмм: 1) вывод символа; 2) очистка экрана

;  **  1) Вывод символа   **
;Вывод символа c помощью данного драйвера инициируется вызовом в программе LCALL LCDPRINT
;Перед вызовом вывода символа регистр R0 должен содержать адрес DDRAM по которому
;будет выводиться символ, регистр R1 должен содержать код выводимого символа
;Выходные параметры: отстутствуют
;В ходе вывода символа используются: регистры(DPP,DPTR,A,R0,R1)

;  **  2) Очистка экрана  **
;Очистка экрана инициируется вызовом в программе LCALL CLEARLCD
;Входные параметры: отсутствуют
;Выходные параметры: отсутствуют
;В очистки экрана используются: регистры(DPP,DPTR,A)

LCDPRINT::
	mov DPP,#08h		;настройка странчиного указаетля 
	MOV DPTR,#0006H		;настроика указателя на регистр ЖКИ C_IND
	MOV A,#08H         
	movX @DPTR,A		;Установка режима записи команды (RW=0,RS=0)
	
	MOV DPTR,#0001H		;настройка указателя на регистр команд/данных DATA_IND
	MOV A,R0          	;R0 - соедержит адрес DDRAM по которому будет выведен символ
	movX @DPTR,A		;запись адреса в регистра DATA_IND
	
	MOV DPTR,#0006H		;настроика указателя на регистр ЖКИ C_IND
	MOV A,#09H          	;Фиксация данных регистра DATA_IND на шине ЖКИ (E=1)
	movX @DPTR,A
	lcall LCDPAUSE 		;организация паузы (ожидание установки сигналов)
	
	MOV DPTR,#0006H		;настроика указателя на регистр ЖКИ C_IND
	MOV A,#08H         
	movX @DPTR,A		;запись команды спаду (E=0)

		
	MOV DPTR,#0006H		;настроика указателя на регистр ЖКИ C_IND
	MOV A,#0CH         	
	movX @DPTR,A		;установка режима записи данных (RW=0,RS=1)
	
	MOV DPTR,#0001H		;настройка указателя на регистр команд/данных DATA_IND
	MOV A,R1         	;R1-содержит код символа для вывода
	movX @DPTR,A		;запись кода символа в регистр DATA_IND

	MOV DPTR,#0006H		;настроика указателя на регистр ЖКИ C_IND
	MOV A,#0DH         	;Фиксация данных регистра DATA_IND на шине ЖКИ (E=1)
	movX @DPTR,A
	
	lcall LCDPAUSE 

	MOV DPTR,#0006H		;настроика указателя на регистр ЖКИ C_IND
	MOV A,#08H
	movX @DPTR,A		;запись данных по спаду (E=0)
	
	RET

CLEARLCD::
	mov DPP,#08h
	MOV DPTR,#0006H
	MOV A,#08H         ;Запись команды
	movX @DPTR,A
	
	MOV A,#09H         ;Разрешить
	movX @DPTR,A
	
	MOV DPTR,#0001H
	MOV A,#01H         ;01h-код команды очистки экрана
	movX @DPTR,A
	lcall LCDPAUSE 

	MOV DPTR,#0006H
	MOV A,#08H         ;Запись команды
	movX @DPTR,A
	
	RET	

LCDPAUSE:: 		;организация программной паузы                            	
        PUSH	PSW
	SETB	RS0   			
       	CLR	RS1
        MOV	R1,#9CH			
LCDM1::
        MOV	R0,#92H
LCDM2::
        DJNZ	R0,LCDM2
        DJNZ	R1,LCDM1
        POP	PSW
        RET	
	
